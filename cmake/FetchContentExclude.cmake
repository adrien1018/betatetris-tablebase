include(FetchContent)

function(FetchContent_MakeAvailable_One dep exclude)
  message(STATUS "Fetching ${dep} (exclude: ${exclude})")
  FetchContent_GetProperties(${dep})
  if(NOT ${dep}_POPULATED)
    FetchContent_Populate(${dep})
    if(${dep} MATCHES "^(spdlog|websocketpp)$" AND NOT DEFINED ${dep}_PATCHED)
      # hack spdlog (for deadlock elimination) & websocketpp (for integration & C++20)
      execute_process(COMMAND patch -sp1 -d ${${dep}_SOURCE_DIR} INPUT_FILE ${PROJECT_SOURCE_DIR}/patches/${dep}.patch)
      message(STATUS "Patched ${dep}")
      set(${dep}_PATCHED ON CACHE INTERNAL "")
    endif()
    if(exclude)
      add_subdirectory(${${dep}_SOURCE_DIR} ${${dep}_BINARY_DIR} EXCLUDE_FROM_ALL)
    else()
      add_subdirectory(${${dep}_SOURCE_DIR} ${${dep}_BINARY_DIR})
    endif()
  endif()
endfunction()

function (FetchContent_MakeAvailable_Exclude)
  foreach(dep IN LISTS ARGV)
    FetchContent_MakeAvailable_One(${dep} 1)
  endforeach()
endfunction()

function (FetchContent_MakeAvailable_Include)
  foreach(dep IN LISTS ARGV)
    FetchContent_MakeAvailable_One(${dep} 0)
  endforeach()
endfunction()
